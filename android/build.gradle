def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

group = "ai.pyck.nimmsta.nimmsta"
version = "1.0.0"

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.1.4")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.22")
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://maven.nimmsta.com/repository/nimmsta-core-release/'
            credentials {
                username = localProperties.getProperty('nimmsta.username')
                password = localProperties.getProperty('nimmsta.password')
            }
            authentication {
                basic(BasicAuthentication)
            }
            metadataSources {
                mavenPom()
                artifact()
            }
        }
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    namespace = "ai.pyck.nimmsta.nimmsta"

    compileSdk = 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }

    defaultConfig {
        minSdk = 26
    }

    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
    }
}

dependencies {
    api("com.nimmsta.core:shared-android:7.0.7825") {
        transitive = true
        changing = true

        exclude group: 'io.ktor', module: 'ktor-server-core'
        exclude group: 'io.ktor', module: 'ktor-server-netty'
        exclude group: 'io.ktor', module: 'ktor-server-websockets'
        exclude group: 'io.ktor', module: 'ktor-server-cors'
        exclude group: 'io.ktor', module: 'ktor-client-android'
    }
}